MODULE test =
BEGIN

LIBRARY 'testharness.lib';

FIELD
    fld0 = [0,0,4,0],
    flds = SET
            fld1 = [0,4,4,0],
            fld2 = [1,0,4,0],
            fld3 = [1,4,4,0]
           TES;
OWN
    blk : BLOCK [2,BYTE] FIELD (fld0,flds),
    vec : VECTOR [4] INITIAL (1,2,3,4),
    yesno : VECTOR [2] INITIAL(UPLIT(%ASCIZ'NO'),UPLIT(%ASCIZ'YES'));

BIND
    gbword = blk : WORD,
    gbwarr = blk : VECTOR [,WORD];
        
test_setup(numcases=10);

ROUTINE testref (caseno, v : REF VECTOR) =
BEGIN
    IF .caseno EQL 1 THEN
        RETURN .v
    ELSE IF .caseno LEQ 5 THEN
        RETURN .v[.caseno-2]
    ELSE
        RETURN 0
END;

ROUTINE testbvec (idx, bvarg) =
BEGIN
    LOCAL bv : BITVECTOR[%BPVAL] INITIAL(.bvarg);
    RETURN .bv[.idx];
END;

GLOBAL ROUTINE runtest (caseno) : NOVALUE =
BEGIN

    IF .caseno EQL 1 THEN
        test_output(.caseno, '%s',
                    .yesno[vec EQLA testref(.caseno, vec)])
!!  1 YES
    ELSE IF .caseno LEQ 5 THEN
        test_output(.caseno, '%ld', testref(.caseno, vec))
!!  2 1
!!  3 2
!!  4 3
!!  5 4
    ELSE IF .caseno LEQ 9 THEN
        test_output(.caseno, '%d', testbvec(.caseno-5, %B'1010'))
!!  6 1
!!  7 0
!!  8 1
!!  9 0
    ELSE
    BEGIN
        BIND
            bword = blk : WORD,
            bwarr = blk : VECTOR [,WORD];
        blk [fld0] = .vec[0];
        blk [fld1] = .vec[1];
        blk [fld2] = .vec[2];
        blk [fld3] = .vec[3];
        test_output(.caseno, '%x,%x,%x,%x',
                    .bword, .gbword, .bwarr[0], .gbwarr[0]);
!! 10 4321,4321,4321,4321
    END;
    
END;

END
ELUDOM
