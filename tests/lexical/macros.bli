MODULE test =
BEGIN

LIBRARY 'testharness.lib';

COMPILETIME
    ctval = 0;
    
MACRO
    nullmacro = %,
    macro_noparams = 1234 %,
    doswap(a, b) = (LOCAL tmp; tmp = .a; a = .b; b = .tmp)%,
    condmacro(x)[] = %STRING(x), condmacro(%REMAINING) %,
    test_cases(caseindex)[fmt, vals] =
        %ASSIGN(ctval, %COUNT+1)
        [ctval] : test_output(caseindex, fmt, %REMOVE(vals)) %,
    macro_argcount(x)[] = %LENGTH %,
    expandthis = %ASCIZ'expanded' %,
    expander(mname) = %EXPAND %QUOTE mname %,
    testlitname = hello%,
    test_exititer1[a] = %IF %COUNT EQL 2 %THEN a %EXITITERATION %FI '.' %,
    test_exititer[] = %ASCIZ %STRING(test_exititer1(%REMAINING)) %,
    test_exitmacro(x) = UPLIT(%ASCIZ %STRING( 'exit ',
        %IF x %THEN 'early')) %EXITMACRO %FI 'normally')) %,
    testunquote = %UNQUOTE testlitname %;

KEYWORDMACRO
    copyvector(dest, src, n=1) =
        INCR i FROM 0 TO (n)-1 DO dest[.i] = .src[.i] %,
    test_errormacro(generror=0) =
        %IF generror %THEN %ERRORMACRO('%ERRORMACRO invoked') %FI %;

LITERAL
    vecsize = 10,
    testunquote = 99;

OWN
    vec1    : VECTOR [vecsize],
    vec2    : VECTOR [vecsize],
    yesno : VECTOR [2] INITIAL(UPLIT(%ASCIZ'NO'),UPLIT(%ASCIZ'YES'));

test_setup(numcases=10);

GLOBAL ROUTINE runtest (caseno) : NOVALUE =
BEGIN
    LOCAL
        x, y, vecseql;
    
    IF .caseno EQL 1 THEN
    BEGIN
        x = 999;
        y = 888;
        doswap(x, y);
        test_output(.caseno, 'x=%d, y=%d', .x, .y);
!! 1 x=888, y=999
    END
    ELSE IF .caseno EQL 2 THEN
    BEGIN
        INCR i FROM 0 TO vecsize-1 DO vec1[.i] = .i+1;
        copyvector(src=vec1, dest=vec2, n=vecsize);
        vecseql = (INCR i FROM 0 TO vecsize-1 DO
                IF .vec1[.i] NEQ .vec2[.i] THEN EXITLOOP 0; 1);
        test_output(.caseno, '%s', .yesno[.vecseql]);
!! 2 YES
    END
    ELSE
    CASE .caseno-2 FROM 1 TO 8 OF
    test_cases(.caseno,
                'nullmacro is null: %s', .yesno[%NULL(nullmacro)],
!! 3 nullmacro is null: YES
                '%d', macro_noparams,
!! 4 1234
                'count=%d', macro_argcount(1,2,3,4,5),
!! 5 count=5
                '%%EXPAND test: %s', UPLIT(expander(%QUOTE expandthis)),
!! 6 %EXPAND test: expanded
                'hello=%d', hello,
!! 7 hello=99
                '"%s"', UPLIT(test_exititer(%EXPLODE('abcde'))),
!! 8 "..c.."
                '"%s"', test_exitmacro(0),
!! 9 "exit normally"
                '"%s"', test_exitmacro(1));
!! 10 "exit early"

    test_errormacro(generror=0)
    test_errormacro(generror=1)
!! cerr %BLISS-E-USRERR, %ERRORMACRO invoked
        
END;

END
ELUDOM
